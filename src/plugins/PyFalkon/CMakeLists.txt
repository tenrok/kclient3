# Enable policy to run automoc on generated files.
if(POLICY CMP0071)
  cmake_policy(SET CMP0071 NEW)
endif()

# Get all relevant Qt include dirs, to pass them on to shiboken.
get_property(QT_CORE_INCLUDE_DIRS TARGET Qt5::Core PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
get_property(QT_GUI_INCLUDE_DIRS TARGET Qt5::Gui PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
get_property(QT_SQL_INCLUDE_DIRS TARGET Qt5::Sql PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
get_property(QT_WIDGETS_INCLUDE_DIRS TARGET Qt5::Widgets PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
get_property(QT_NETWORK_INCLUDE_DIRS TARGET Qt5::Network PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
get_property(QT_WEBENGINECORE_INCLUDE_DIRS TARGET Qt5::WebEngineCore PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
get_property(QT_WEBENGINEWIDGETS_INCLUDE_DIRS TARGET Qt5::WebEngineWidgets PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
set(QT_INCLUDE_DIRS
    ${QT_CORE_INCLUDE_DIRS}
    ${QT_GUI_INCLUDE_DIRS}
    ${QT_SQL_INCLUDE_DIRS}
    ${QT_WIDGETS_INCLUDE_DIRS}
    ${QT_NETWORK_INCLUDE_DIRS}
    ${QT_WEBENGINECORE_INCLUDE_DIRS}
    ${QT_WEBENGINEWIDGETS_INCLUDE_DIRS}
)
set(INCLUDES "")
foreach(INCLUDE_DIR ${QT_INCLUDE_DIRS})
    list(APPEND INCLUDES "-I${INCLUDE_DIR}")
endforeach()
get_property(KClientPrivate_INCLUDE_DIRS TARGET KClientPrivate PROPERTY INCLUDE_DIRECTORIES)
foreach(INCLUDE_DIR ${KClientPrivate_INCLUDE_DIRS})
    list(APPEND INCLUDES "-I${INCLUDE_DIR}")
endforeach()

# Set up the options to pass to shiboken.
set(GLOBAL_HEADER ${CMAKE_CURRENT_SOURCE_DIR}/pykclient_global.h)
set(TYPESYSTEM_FILE ${CMAKE_CURRENT_SOURCE_DIR}/typesystem_pykclient.xml)

set(SHIBOKEN_OPTIONS --generator-set=shiboken --enable-parent-ctor-heuristic
    --enable-pyside-extensions --enable-return-value-heuristic --use-isnull-as-nb_nonzero
    --avoid-protected-hack
    ${INCLUDES}
    -T${PYSIDE_TYPESYSTEMS}
    --output-directory=${CMAKE_CURRENT_BINARY_DIR}
    --api-version="${Qt5_VERSION_MAJOR}.${Qt5_VERSION_MINOR}"
)

# Specify which sources will be generated by shiboken, and their dependencies.
set(GENERATED_SOURCES
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/pykclient_module_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/webview_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/tabbedwebview_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/webpage_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/webhittestresult_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/desktopfile_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/plugininterface_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/loadrequest_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/qz_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/desktopnotificationsfactory_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/externaljsobject_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/pluginproxy_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/plugins_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/plugins_plugin_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/pluginspec_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/qtsingleapplication_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/squeezelabelv1_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/squeezelabelv2_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/lineedit_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/sidewidget_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/webtab_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/mainapplication_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/datapaths_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/settings_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/autosaver_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/browserwindow_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/pageformdata_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/passwordentry_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/passwordbackend_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/autofill_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/passwordmanager_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/bookmarkitem_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/bookmarkstools_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/bookmarks_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/bookmarksmodel_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/bookmarksfoldersmenu_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/bookmarksfoldersbutton_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/cookiemanager_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/cookiejar_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/downloaditem_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/downloadmanager_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/downloadmanager_downloadinfo_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/history_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/history_historyentry_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/historyitem_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/historymodel_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/locationbar_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/locationbar_loadaction_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/navigationbar_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/networkmanager_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/searchenginesdialog_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/searchenginesmanager_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/searchenginesmanager_engine_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/sidebarmanager_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/sidebarinterface_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/webinspector_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/combotabbar_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/tabbar_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/tabcontextmenu_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/tabicon_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/tabicon_data_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/tabmodel_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/tabmrumodel_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/tabstackedwidget_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/tabtreemodel_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/tabwidget_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/searchtoolbar_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/checkboxdialog_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/qzsettings_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/statusbar_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/abstractbuttoninterface_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/abstractbuttoninterface_clickcontroller_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/clickablelabel_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/delayedfilewatcher_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/iconprovider_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/qztools_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/sqlqueryjob_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/sqldatabase_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/toolbutton_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/wheelhelper_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/menu_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/action_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/urlinterceptor_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient/extensionschemehandler_wrapper.cpp
)
set(GENERATED_SOURCES_DEPENDENCIES
    ${GLOBAL_HEADER}
    ${TYPESYSTEM_FILE}
)

# Add custom target to run shiboken.
add_custom_command(OUTPUT ${GENERATED_SOURCES}
                    COMMAND Shiboken2::shiboken2
                    ${SHIBOKEN_OPTIONS} ${GLOBAL_HEADER} ${TYPESYSTEM_FILE}
                    DEPENDS ${GENERATED_SOURCES_DEPENDENCIES}
                    WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
                    COMMENT "Running generator for ${TYPESYSTEM_FILE}.")

# We need to include the headers for the module bindings that we use.
set(PYSIDE_ADDITIONAL_INCLUDES "")
get_target_property(PYSIDE_INCLUDE_DIRS PySide2::pyside2 INTERFACE_INCLUDE_DIRECTORIES)
foreach(INCLUDE_DIR ${PYSIDE_INCLUDE_DIRS})
    list(APPEND PYSIDE_ADDITIONAL_INCLUDES "${INCLUDE_DIR}/QtCore")
    list(APPEND PYSIDE_ADDITIONAL_INCLUDES "${INCLUDE_DIR}/QtGui")
    list(APPEND PYSIDE_ADDITIONAL_INCLUDES "${INCLUDE_DIR}/QtNetwork")
    list(APPEND PYSIDE_ADDITIONAL_INCLUDES "${INCLUDE_DIR}/QtPrintSupport")
    list(APPEND PYSIDE_ADDITIONAL_INCLUDES "${INCLUDE_DIR}/QtWidgets")
    list(APPEND PYSIDE_ADDITIONAL_INCLUDES "${INCLUDE_DIR}/QtWebChannel")
    list(APPEND PYSIDE_ADDITIONAL_INCLUDES "${INCLUDE_DIR}/QtWebEngineCore")
    list(APPEND PYSIDE_ADDITIONAL_INCLUDES "${INCLUDE_DIR}/QtWebEngineWidgets")
endforeach()

set(PyKClient_SRCS
    pythonplugin.cpp
    ${GENERATED_SOURCES}
)

add_library(PyKClient MODULE ${PyKClient_SRCS})
install(TARGETS PyKClient DESTINATION ${KCLIENT_INSTALL_PLUGINDIR})

target_include_directories(PyKClient
    PRIVATE
    ${PYSIDE_ADDITIONAL_INCLUDES}
    ${CMAKE_SOURCE_DIR}
    ${CMAKE_CURRENT_BINARY_DIR}/PyKClient
)

target_link_libraries(PyKClient
    PRIVATE
    KClientPrivate
    Shiboken2::libshiboken
    PySide2::pyside2
    Python3::Python
)

# Same as CONFIG += no_keywords to avoid syntax errors in object.h due to the usage of the word Slot
target_compile_definitions(PyKClient PRIVATE QT_NO_KEYWORDS)

if(BUILD_TESTING)
    add_subdirectory(autotests)
endif()
